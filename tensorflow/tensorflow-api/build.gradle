group "ai.djl.tensorflow"

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    exclusion
}

dependencies {
    api "org.bytedeco:javacpp:1.5.3"
    api "com.google.protobuf:protobuf-java:3.8.0"
    api "org.tensorflow:tensorflow-core-api:0.2.0-SNAPSHOT"
    api "org.tensorflow:tensorflow-tools:0.1.0-SNAPSHOT"

    exclusion "org.bytedeco:javacpp:1.5.3"
    exclusion "com.google.protobuf:protobuf-java:3.8.0"
}

jar {
    from {
        (configurations.compileClasspath - configurations.exclusion).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL release for TensorFlow core api"
                description = "Deep Java Library (DJL) release for TensorFlow core api"
                url = "http://www.djl.ai/tensorflow/${project.name}"

                withXml {
                    Node pomNode = asNode()
                    pomNode.dependencies."*".findAll() {
                        it.artifactId.text().startsWith("tensorflow-")
                    }.each() {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}
